buildscript {
    repositories {
        mavenLocal()
        jcenter()
        maven {url 'https://repo.grails.org/grails/core'}
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
    }
}

version xhReleaseVersion
group 'io.xh'

apply plugin:'idea'
apply plugin:'org.grails.grails-plugin'
apply plugin:'org.grails.grails-gsp'

repositories {
    mavenLocal()
    jcenter()
    maven {url 'https://repo.grails.org/grails/core'}
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

dependencies {
    // Grails 4.0.3 REST Plugin Defaults
    developmentOnly("org.springframework.boot:spring-boot-devtools")
    compile "org.springframework.boot:spring-boot-starter-logging"
    compile "org.springframework.boot:spring-boot-autoconfigure"
    compile "org.grails:grails-core"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile "org.springframework.boot:spring-boot-starter-tomcat"
    compile "org.grails:grails-plugin-url-mappings"
    compile "org.grails:grails-plugin-rest"
    compile "org.grails:grails-plugin-codecs"
    compile "org.grails:grails-plugin-interceptors"
    compile "org.grails:grails-plugin-services"
    compile "org.grails:grails-plugin-datasource"
    compile "org.grails:grails-plugin-databinding"
    compile "org.grails:grails-web-boot"
    compile "org.grails:grails-logging"
    compile "org.grails.plugins:async"
    compile "org.grails.plugins:events"
    compile "org.grails.plugins:hibernate5"
    compileOnly "io.micronaut:micronaut-inject-groovy"
    console "org.grails:grails-console"
    profile "org.grails.profiles:rest-api"

    runtime "org.glassfish.web:el-impl:2.1.2-b03"
    runtime "com.h2database:h2"
    runtime "org.apache.tomcat:tomcat-jdbc"
    runtime "javax.xml.bind:jaxb-api:2.3.0"

    // Hoist Additions
    compile "org.codehaus.groovy:groovy-dateutil:2.5.8"
    provided "org.grails:grails-plugin-domain-class"
    compile 'org.grails.plugins:mail:3.0.0'

    compile "org.hibernate:hibernate-core:5.4.14.Final"
    compile "net.sf.ehcache:ehcache:2.10.6"

    compile "org.apache.poi:poi:3.17"
    compile "org.apache.poi:poi-ooxml:3.17"
    compile "org.apache.poi:poi-ooxml-schemas:3.17"
    compile "org.apache.poi:ooxml-schemas:1.3"

    compile "org.springframework:spring-websocket"
    compile "org.apache.httpcomponents:httpclient:4.5.6"
    compile 'org.jasypt:jasypt:1.9.2'
    compile "io.reactivex.rxjava2:rxjava:2.1.3"
    compile "com.grack:nanojson:1.4"
    compile 'commons-io:commons-io:2.6'
}

bootRun {
    jvmArgs('-Dspring.output.ansi.enabled=always')
    sourceResources sourceSets.main
}

bootJar.enabled = false


//------------------------
// Maven publishing
// This is a modified version of the gradle configs setup by the Grails plugin publishing plugin
// https://github.com/grails/grails-core/blob/master/grails-gradle-plugin/src/main/groovy/org/grails/gradle/plugin/publishing/GrailsCentralPublishGradlePlugin.groovy
// The default behavior assumes publishing of non-snapshot builds only to the paid (if private) Bintray system
//------------------------
apply plugin:'maven-publish'
publishing {
    publications {
        hoistCore(MavenPublication) {
            artifactId 'hoist-core'

            pom.withXml {
                Node pomNode = asNode()
                if (pomNode.dependencyManagement) {
                    pomNode.dependencyManagement[0].replaceNode {}
                }

                pomNode.children().last() + {
                    delegate.name 'hoist-core'
                    delegate.description "Extremely Heavy Industry's toolkit for enterprise web applications."
                    delegate.url 'https://xh.io'
                    delegate.organization {
                        delegate.name 'Extremely Heavy Industries'
                        delegate.url 'https://xh.io'
                    }
                    delegate.scm {
                        delegate.url "https://github.com/xh/hoist-core"
                        delegate.connection "scm:git@github.com:xh/hoist-core.git"
                        delegate.developerConnection "scm:git@github.com:xh/hoist-core.git"
                    }
                    delegate.issueManagement {
                        delegate.system "GitHub"
                        delegate.url "https://github.com/xh/hoist-core/issues"
                    }
                    delegate.developers {
                        delegate.developer {
                            delegate.id 'xh'
                            delegate.name 'Extremely Heavy Industries'
                            delegate.email 'info@xh.io'
                        }
                    }
                }

                pomNode.dependencies.dependency.findAll {
                    it.version.text().isEmpty()
                }.each {
                    it.replaceNode {}
                }
            }

            from components.java
            artifact sourcesJar
            artifact javadocJar

            def groovyOutputDir = sourceSets.main.output.classesDirs.files.find{it.path.contains('/groovy/')}
            artifact source: "${groovyOutputDir}/META-INF/grails-plugin.xml",
                    classifier: 'plugin',
                    extension: 'xml'

            repositories {
                maven {
                    def repoEndpoint = version.endsWith('-SNAPSHOT') ? 'snapshots' : 'releases'
                    url "https://repo.xh.io/content/repositories/$repoEndpoint/"
                    credentials {
                        username project.findProperty('xhRepoDeployUser')
                        password project.findProperty('xhRepoDeployPassword')
                    }
                }
            }
        }
    }
}

task publishHoistCore(dependsOn: 'publishHoistCorePublicationToMavenRepository') {
    group 'xhio'
    description 'Publishes a build to repo.xh.io.'
    doLast {
        println "Hoist $version published to repo.xh.io!"
    }
}
